#
# SConscript for src directory
    #
import subprocess
import os
import platform
import json
import hashlib
import re

system = platform.system().lower()

Import('rootenv')

localenv = rootenv.Clone()

compiler_version = None
if localenv["CC"] == "gcc":
    compiler_version = subprocess.check_output("gcc --version", shell=True)
elif localenv["CC"] == "cl":
    compiler_version = subprocess.check_output("cl", stderr=subprocess.STDOUT, shell=True)

def CreateProgramMetadataGenerator(config):
    def GenerateProgramMetadata(target, source, env):
        flags = dict()
        flags["CFLAGS"] = str(env["CFLAGS"])
        flags["LINKFLAGS"] = str(env["LINKFLAGS"])

        executable = source[0]
        executable_file = open(executable.abspath, "rb")
        executable_sha256 = hashlib.sha256(executable_file.read()).hexdigest()
        executable_file.close()

        stripped = source[1]
        if stripped.abspath != executable.abspath:
            stripped_file = open(stripped.abspath, "rb")
            stripped_sha256 = hashlib.sha256(stripped_file.read()).hexdigest()
            stripped_file.close()
        else:
            stripped_sha256 = None

        debug = source[2]
        if debug.abspath != executable.abspath:
            debug_file = open(debug.abspath, "rb")
            debug_sha256 = hashlib.sha256(debug_file.read()).hexdigest()
            debug_file.close()
        else:
            debug_sha256 = None

        obj_sha256s = dict()
        if len(source) > 3:
            for obj in source[3:]:
                obj_file = open(obj.abspath, "rb")
                obj_sha256 = hashlib.sha256(obj_file.read()).hexdigest()
                obj_file.close()
                obj_sha256s[obj.name] = obj_sha256

        base_name = re.sub(env["PROGSUFFIX"] + "$", '', executable.name)

        metadata = dict()
        metadata["configName"] = config
        metadata["baseName"] = base_name
        metadata["flags"] = flags
        metadata["platform"] = system
        metadata["compiler"] = str(compiler_version)
        metadata["machine"] = platform.machine().lower()
        metadata["objectSha256s"] = obj_sha256s
        metadata["executableSha256"] = executable_sha256
        metadata["debugSha256"] = debug_sha256
        metadata["strippedSha256"] = stripped_sha256
        metadata["executableSuffix"] = env["PROGSUFFIX"]
        metadata["objectSuffix"] = env["OBJSUFFIX"]

        json_output = open(target[0].abspath, "w")
        json.dump(metadata, json_output)
        json_output.flush()
        json_output.close()

        return None

    return GenerateProgramMetadata

def AddDefaultFlags(env):
    if env['CC'] == 'cl':
        env.Append(CFLAGS = '/TP')
        env.Append(LINKFLAGS = '/INCREMENTAL:NO')

def AddDebugFlag(env):
    if env['CC'] == 'gcc':
        env.Append(CFLAGS = '-g')
    elif env['CC'] == 'cl':
        env["PDB"] = "${TARGET}.debug"
        env.Append(CFLAGS = '/MDd')

def AddPieFlag(env):
    if system == 'linux':
        if env['CC'] == 'gcc':
            env.Append(CFLAGS = '-fPIE')
            env.Append(LINKFLAGS = '-pie')

def DisablePieFlag(env):
    if system == 'darwin':
        if env['CC'] == 'gcc':
            env.Append(LINKFLAGS = '-Wl,-no_pie')

def DebugNoOptDynamic(env):
    AddDefaultFlags(env)
    AddDebugFlag(env)
    DisablePieFlag(env)

def DebugNoOpDynamicPie(env):
    AddDefaultFlags(env)
    AddDebugFlag(env)
    AddPieFlag(env)

configs = list()
configs.append(("debug-noopt-dynamic", DebugNoOptDynamic, ['darwin', 'linux', 'windows']))
configs.append(("debug-noopt-dynamic-pie", DebugNoOpDynamicPie, ['darwin', 'linux']))

sources_file = open(File("sources.json").srcnode().abspath, "r")
source_configs = json.load(sources_file)
sources_file.close()

def Executable(env, target, objs, libs):
    if system == 'darwin':
        program = env.Program(target, objs, LIBS=libs)
        debug = env.Command(target + ".debug", program, "dsymutil -f -o $TARGET $SOURCES")
        stripped = env.Command(target + ".nosym", program, "strip -o $TARGET $SOURCES")
        return [program, stripped, debug]
    elif system == 'linux':
        program = env.Program(target, objs, LIBS=libs)
        stripped = env.Command(target + ".nosym", program, "strip -o $TARGET $SOURCES")
        return [program, stripped, program]
    elif system == 'windows':
        program = env.Program(target, objs, LIBS=libs)
        return [program[0], program[0], program[1]]
    else:
        raise RuntimeError("Unknown platform")

localenv.AddMethod(Executable, "Executable")

for config in configs:
    if not system in config[2]:
        continue

    loopenv = localenv.Clone()
    config[1](loopenv)

    saved_objs = dict()

    for source_config in source_configs:
        if "platforms" in source_config and system not in source_config["platforms"]:
            continue

        objs = []
        for source in source_config["sources"]:
            if source in saved_objs:
                objs.append(saved_objs[source])
            else:
                objs.append(loopenv.Object("${SOURCE.filebase}" + "-" + config[0] + "$OBJSUFFIX", source))

        if system in source_config["platform_sources"]:
            for source in source_config["platform_sources"][system]:
                if source in saved_objs:
                    objs.append(saved_objs[source])
                else:
                    objs.append(loopenv.Object("${SOURCE.filebase}" + "-" + config[0] + "$OBJSUFFIX", source))

        libs = []
        if system in source_config["libs"]:
            for lib in source_config["libs"][system]:
                libs.append(lib)

        executables = loopenv.Executable(source_config["name"] + "-" + config[0], objs, libs=libs)

        metadata_sources = list()
        metadata_sources.append(executables)
        metadata_sources.append(objs)
        loopenv.Command(source_config["name"] + "-" + config[0] + ".json", metadata_sources, CreateProgramMetadataGenerator(config[0]))

# vim: ft=python
